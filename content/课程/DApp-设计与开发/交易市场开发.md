---
title: 交易市场开发
---
> 发行一个符合 ERC-20 标准的测试 Token，要求：
>
> - 总量：一亿
> - 精度：18
> - 名称：Fake USDT in CBI
> - 简称：cUSDT


> 发行一个符合 ERC-721 标准的测试 Token，要求如下：
>
> - 名称：NFTMarketplace
> - 简称：NFTM
> - tokenid 自增
> - 用户在网页上传图片提交之后，自动发行给用户（需要前端配合）


>开发一个 NFT 交易市场，功能如下：
>
>- 用户在创建 NFT 时，可以指定价格（以 cUSDT 计价）
>- NFT 所有者可以修改上架的 NFT 价格
>- NFT 信息上传到 IPFS
>- 用户可以在 NFT 市场以一定的价格购买 NFT


[OpenZeppelin Wizard](https://wizard.openzeppelin.com)
> Wizard OpenZeppelin 是一个交互式的智能合约生成器，它允许开发者通过提供参数和所需的功能来构建智能合约。这个工具是 OpenZeppelin 项目的一部分，旨在帮助开发者以更高效和安全的方式创建去中心化应用（DApps）的智能合约。OpenZeppelin 是一个知名的开源项目，提供了经过严格测试和社区审查的智能合约库，以支持安全、标准化的智能合约开发。
>
>OpenZeppelin 还提供了其他工具和服务，如 OpenZeppelin Defender，这是一个安全平台，用于监控智能合约的风险和行为，检测威胁，并提供自动响应和解决问题的工具。此外，OpenZeppelin 还提供了一个名为 Contracts Wizard 的服务，它是一个用于 Solidity 的智能合约构建器，允许用户通过交互式方式构建合约。
>
>使用 Wizard OpenZeppelin，开发者可以利用 OpenZeppelin 提供的标准化和经过测试的代码组件，快速搭建出满足特定需求的智能合约，同时降低开发过程中的风险。

[toc]



# 一、初始化工程

1. 创建项目
```bash
mkdir nft-market
cd nft-market
npx hardhat init
# 一直回车即可
```
2. 删除初始文件
```bash
rm contracts/Lock.sol
```



# 二、创建 ERC20-USDT 合约

## 1. 新建文件 `erc20-usdt`，并写入初始代码

1. 复制代码
	- 打开 [OpenZeppelin wizard](https://wizard.openzeppelin.com/#erc20) 选择 
		- ERC20
		- （SETTING / Name）cUSDT
		- （SETTING / symbol）cUSDT
		- （FEATURES）Mintable
		- （ACCESS CONTROL）Ownable
	- 

2. 创建文件并粘贴代码
```solidity title="erc20-usdt.sol" {8,10}
// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts ^5.0.0
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract cUSDT is ERC20, Ownable {
    constructor(address initialOwner)
        ERC20("fake usdt in cbi", "cUSDT")
        Ownable(initialOwner)
    {}

    function mint(address to, uint256 amount) public onlyOwner {
        _mint(to, amount);
    }
}
```
> - 合约名称可能会默认大写 `CUSDT`，需要改为 `cUSDT`
> - `ERC20("cUSDT", "cUSDT")` 改为 ` ERC20("fake usdt in cbi", "cUSDT")`
1. 安装库
```bash
npm install @openzeppelin/contracts
```

## 2. 在 Remix 中打开

1. 将本地项目共享给 Remix
```bash
dir=$(pwd)
open -a "Google Chrome" https://remix.ethereum.org/
remixd -s $dir --remix-ide https://remix.ethereum.org
```

## 3. 部署合约并测试

1. 打开 `deploy & run transaction`
2. 复制 `ACCOUNT` 地址
3. 粘贴 `CONTRCT` -> `deploy`
	-  `totalSupply`将会显示 `0`
	- `mint` 粘贴自己的刚复制的地址，并输入 `100000000000000000000000000`
	- `totalSupply`将会显示  `100000000000000000000000000`

## 4. 修改代码

```solidity title="erc20-usdt.sol"
pragma solidity ^0.8.20;
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
contract cUSDT is ERC20{
	constructor(
	) ERC20("fake usdt in cbi", "cUSDT") {
		_mint(msg.sender, 1 * 10 ** 8 * 10 ** 18);
	}
}
```

## 5. 重新部署合约，并测试

1. 打开 `deploy & run transaction`
2. 复制 `ACCOUNT`
3. 粘贴 `CONTRCT` -> `deploy`
4. `balanceOf` 将会显示   `100000000000000000000000000`
5. `totalSupply`将会显示  `100000000000000000000000000`



# 三、创建 ERC721-NFT 合约

## 1. 新建文件 `erc721-nft`，并写入初始代码

1. 复制代码
	- 打开 [OpenZeppelin wizard](https://wizard.openzeppelin.com/#erc721) 选择 
		- ERC721
		- （SETTING）
			- （Name）`NFTM`
			- （symbol）`NFTM`
			- （Base URI）`https://cbisimple.com/`
		- （FEATURES）`Mintable` & `Auto Increment Ids` & `Enumerable`
		- （ACCESS CONTROL）`Ownable`

## 2. 部署合约并测试

1. 打开 `deploy & run transaction`
2. 复制 `ACCOUNT`地址
3. 粘贴 `CONTRCT` -> `Deploy`，单击 `Deploy`
	-  `totalSupply`将会显示 `0`
	- `safeMint`粘贴自己的地址，单击 `transfer`
	- `totalSupply`将会显示  `1`
	- `TokenURI` 输入 `0` 将会显示 `https://cbisimple.com/0`



# 四、创建 NFT-Market 合约

## 1. 创建合约

[完整代码](#nft-market)

```solidity title="nft-market.sol"
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;
import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "@openzeppelin/contracts/interfaces/IERC721.sol";

contract Market{
	IERC20 public erc20;
	IERC721 public erc721;
	constructor(address _erc20,address _erc721){
		require(_erc20 != address(0), "zero address");
		require(_erc721 != address(0), "zero address");
		erc20 = IERC20(_erc20);
		erc721 = IERC721(_erc721);
	}
}
```

## 2. 部署合约

### 2.1 部署 ERC-20

`CONTRACT` 选择 `ERC20`

`Deploy`直接部署



### 2.2 部署 ERC-721

`CONTRACT`选择 `ERC721`

`Deploy`直接部署



### 2.3 部署 NFT-Market

`CONTRACT` 选择 `Market`

复制`erc-20` 和 `erc-721`  到 `Depoly` 部署





## 3. 测试合约

### 3.1 NFTM 合约中

- `safeMint` 测试是否能够 mint 给自己（mint  个给自己）

- `safeTransferFrom` 测试是否能够自动上架
	
	- `from`： 你自己的地址
	
	- `to` ： nft-market 合约地址
	
	- `tokenId` ： `0`,`1`
	
	- `data`： `0x0000000000000000000000000000000000000000000000000001c6bf52634000`
	
	    > 价格的生成方法：
	    >
	    > web3.apdLet(web3.toHex("500000000000000").substr(2),64)
	
- `totalSupply`
	
	> 输出类似于
	>
	> ```bash
	> 0:
	> uint256: 11
	> ```
	>
	> 
	>
	> 

### 3.2 Market 合约中

- `orders`：
  
  - `0`  测试是否能够被查询到
  
  >输出类似于：
  >
  >```bash
  >0:
  >address: seller 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4
  >1:
  >uint256: tokenId 3
  >2:
  >uint256: price 500000000000000
  >```
  
  - `1`  测试是否能够被查询到
  
  >输出类似于：
  >
  >```bash
  >0:
  >address: seller 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4
  >1:
  >uint256: tokenId 4
  >2:
  >uint256: price 500000000000000
  >```

- `getAllNFTs`获取所有的 NFT

    > 输出类似于
    >
    > ```bash
    > 0:
    > tuple(address,uint256,uint256)[]: 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4,3,500000000000000,
    > 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4,4,500000000000000
    > ```
    >
    > 

- `getOrderLength`

    > 输出类似于
    >
    > ```bash
    > 0:
    > uint256: 2
    > ```
    >
    > 

- `getMyNFTs` 输出类似于

    > ```bash
    > 0:
    > tuple(address,uint256,uint256)[]: 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4,9,500000000000000,
    > 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4,10,500000000000000
    > ```
    >
    > 





# 五、附录

## 1. 代码

### erc20-usdt

```solidity title="erc20-usdt.sol"
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract cUSDT is ERC20 {
    constructor() ERC20("fake usdt in cbi", "cUSDT") {
        _mint(msg.sender, 1 * 10 ** 8 * 10 ** 18);
    }
}

```

### erc721-nft

```solidity title="erc721-nft.sol"
// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts ^5.0.0
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract NFTM is ERC721, ERC721Enumerable, Ownable {
    uint256 private _nextTokenId;

    constructor(
        address initialOwner
    ) ERC721("NFTM", "NFTM") Ownable(initialOwner) {}

    function _baseURI() internal pure override returns (string memory) {
        return "https://cbisimple.com/";
    }

    function safeMint(address to) public onlyOwner {
        uint256 tokenId = _nextTokenId++;
        _safeMint(to, tokenId);
    }

    // The following functions are overrides required by Solidity.

    function _update(
        address to,
        uint256 tokenId,
        address auth
    ) internal override(ERC721, ERC721Enumerable) returns (address) {
        return super._update(to, tokenId, auth);
    }

    function _increaseBalance(
        address account,
        uint128 value
    ) internal override(ERC721, ERC721Enumerable) {
        super._increaseBalance(account, value);
    }

    function supportsInterface(
        bytes4 interfaceId
    ) public view override(ERC721, ERC721Enumerable) returns (bool) {
        return super.supportsInterface(interfaceId);
    }

}

```

### nft-market

```solidity title="nft-market.sol"
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "@openzeppelin/contracts/interfaces/IERC721.sol";

contract Market {
    IERC20 public erc20;
    IERC721 public erc721;

    bytes4 internal constant ERC721_RECEIVED = 0x150b7a02;

    // 谁买的，tokenId 是什么，价格是多少
    struct Order {
        address seller;
        uint256 tokenId;
        uint256 price;
    }

    // 为了查询方便，根据 id 查询 Order
    mapping(uint256 => Order) public orderOfId; // token id to order

    // 查询当前交易市场的所有订单
    Order[] public orders;
    mapping(uint256 => uint256) public idToOrderIndex; // token id to idnex in orders

    // 事件（在向链上进行写事件的时候会抛出事件）
    // 成交
    event Deal(address seller, address buyer, uint256 tokenId, uint256 price);
    // 挂单
    event NewOrder(address seller, uint256 tokenId, uint256 price);
    // 改价
    event PriceChanged(
        address seller,
        uint256 tokenId,
        uint256 previousPrice,
        uint256 newPrice
    );
    // 撤单
    event OrderCancelled(address seller, uint256 tokenId);

    constructor(address _erc20, address _erc721) {
        require(_erc20 != address(0), "zero address");
        require(_erc721 != address(0), "zero address");
        erc20 = IERC20(_erc20);
        erc721 = IERC721(_erc721);
    }

    // 购买：购买方转出 USDT，出售方转出 NFT
    function buy(uint256 _tokenId) external {
        address seller = orderOfId[_tokenId].seller;
        address buyer = msg.sender;
        uint256 price = orderOfId[_tokenId].price;
        require(
            erc20.transferFrom(buyer, seller, price),
            "transfer not successful"
        );
        erc721.safeTransferFrom(address(this), buyer, _tokenId);

        removeOrder(_tokenId);

        emit Deal(seller, buyer, _tokenId, price);
    }

    // 取消订单
    function cancelOrder(uint256 _tokenId) external {
        address seller = orderOfId[_tokenId].seller;
        require(msg.sender == seller, "not the seller");
        erc721.safeTransferFrom(address(this), seller, _tokenId);
        removeOrder(_tokenId);
        emit OrderCancelled(seller, _tokenId);
    }

    // 改价格
    function changePrice(uint256 _tokenId, uint256 _newPrice) external {
        address seller = orderOfId[_tokenId].seller;
        require(msg.sender == seller, "not the seller");
        require(_newPrice > 0, "price must be greater than 0");
        uint256 previousPrice = orderOfId[_tokenId].price;
        orderOfId[_tokenId].price = _newPrice;
        Order storage order = orders[idToOrderIndex[_tokenId]];
        // memory 和 storage 区别：storage 存储在链上，memory 存储在内存中
        emit PriceChanged(seller, _tokenId, previousPrice, _newPrice);
        order.price = _newPrice;
        emit PriceChanged(seller, _tokenId, previousPrice, _newPrice);
    }

    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4) {
        uint256 price = toUint256(data, 0);
        require(price > 0, "price must be greater than 0");
        Order memory order = Order(from, tokenId, price);
        // 自动上架
        orders.push(Order(from, tokenId, price));
        orderOfId[tokenId] = Order(from, tokenId, price);
        idToOrderIndex[tokenId] = orders.length - 1;
        emit NewOrder(from, tokenId, price);
        return ERC721_RECEIVED;
    }

    function removeOrder(uint256 _tokenId) internal {
        uint256 index = idToOrderIndex[_tokenId];
        uint256 lastIndex = orders.length - 1;
        if (index != lastIndex) {
            Order storage lastOrder = orders[lastIndex];
            orders[index] = lastOrder;
            idToOrderIndex[lastOrder.tokenId] = index;
        }
        orders.pop();
        delete orderOfId[_tokenId];
        delete idToOrderIndex[_tokenId];
    }

    // https://stackoverflow.com/questions/63252057/how-to-use-bytestouint-function-in-solidity-the-one-with-assembly
    function toUint256(
        bytes memory _bytes,
        uint _start
    ) public pure returns (uint256) {
        require(_start + 32 >= _start, "Market: toUint256_overflow");
        require(_bytes.length >= _start + 32, "Market: toUint256_outOfBounds");
        uint256 tempUint;
        assembly {
            tempUint := mload(add(add(_bytes, 0x20), _start))
        }
        return tempUint;
    }
    function getOrderLength() external view returns (uint256) {
        return orders.length;
    }
    function getAllNFTs() external view returns (Order[] memory) {
        return orders;
    }

    
    function getMyNFTs() external view returns (Order[] memory) {
        uint256 length = orders.length;
        uint256 counter = 0;
        for (uint256 i = 0; i < length; i++) {
            if (orders[i].seller == msg.sender) {
                counter++;
            }
        }
        Order[] memory myNFTs = new Order[](counter);
        uint256 index = 0;
        for (uint256 i = 0; i < length; i++) {
            if (orders[i].seller == msg.sender) {
                myNFTs[index] = orders[i];
                index++;
            }
        }
        return myNFTs;
    }
}
```

