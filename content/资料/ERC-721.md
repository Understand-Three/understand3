---
title: ERC-721
tags:
  - "#Tokens"
---

```yaml
original: 
	standard-en: "https://eips.ethereum.org/EIPS/eip-721"
	  update: 2018-01-24
	standard-zh: "https://learnblockchain.cn/docs/eips/eip-721.html"
```

以太坊上的非同质化代币（NFT）标准，用于创建独一无二的代币，常用于收藏品、艺术品等。

# EIP 721:  非同质化 Token 标准

| 作者                                                         | 状态  | 类型            | 分类 | 创建时间   | 依赖                                                     |
| ------------------------------------------------------------ | ----- | --------------- | ---- | ---------- | -------------------------------------------------------- |
| William Entriken [github.com@phor.net](mailto:github.com@phor.net), Dieter Shirley [dete@axiomzen.co](mailto:dete@axiomzen.co), Jacob Evans [jacob@dekz.net](mailto:jacob@dekz.net), Nastassia Sachs [nastassia.sachs@protonmail.com](mailto:nastassia.sachs@protonmail.com) | Final | Standards Track | ERC  | 2018-01-24 | [165](https://learnblockchain.cn/docs/eips/eip-165.html) |



## 简要说明

非同质化（Non-Fungible Token，以下简称 NFT 或 NFTs）代币标准。

> 译者注： ERC-721 标准提出时，solidity 版本是0.4.20，所有此标准在0.4.20基础上编写。



## 摘要

以下标准允许在智能合约中实现非同质化代币(NFT)的标准API。这一标准提供了跟踪和转移NFT的基本功能。

我们考虑了NFT被个人拥有和交易的使用案例，以及委托给第三方经纪人（third party brokers）/ 钱包（wallets）/ 拍卖人（auctioneers，"operators" ）。NFT可以代表对数字或物理资产的所有权。我们考虑了一个多样化的资产宇宙，并且我们知道你们会想出更多：

- 物理资产 —— 房屋、独特的艺术品
- 虚拟收藏品 —— 独特的小猫图片、收藏卡
- “负价值”资产 —— 贷款、负担和其他责任

总的来说，所有的房屋都是不同的，没有两只小猫是相似的。NFT 是可区分的，你必须分别跟踪每一个的所有权。

## 动机

一个标准接口允许钱包（wallets）/ 代理人（broker）/拍卖应用程序（auction applications ）与以太坊上的任何非同质化代币（NFT）一起工作。我们提供了简单的 ERC-721 智能合约，以及跟踪任意数量的NFT的合约。下面讨论了其他应用程序。

这一标准受到 ERC-20 代币标准的启发，并在 EIP-20 创建后的两年经验基础上建立。EIP-20 不足以跟踪 NFT，因为每个资产都是独特的（非同质化），而一定数量的 Token 中的每一个都是相同的（同质化）。

下面的部分将检查这一标准与 EIP-20之 间的差异。



## 规范

**每个符合ERC-721的合同都必须实现 `ERC721` 和 `ERC165` 接口**（受以下“说明”约束）：

```solidity
pragma solidity ^0.4.20;

/// @title ERC-721非同质化代币标准
/// @dev See https://learnblockchain.cn/docs/eips/eip-721.html
///  Note: ERC-165 接口id 为 0x80ac58cd.
interface ERC721 /* is ERC165 */ {
    /// @dev 当任何NFT的所有权更改时（不管哪种方式），就会触发此事件。
    ///  包括在创建时（`from` == 0）和销毁时(`to` == 0), 合约创建时除外。
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

    /// @dev 当更改或确认NFT的授权地址时触发。
    ///  零地址表示没有授权的地址。
    ///  发生 `Transfer` 事件时，同样表示该NFT的授权地址（如果有）被重置为“无”（零地址）。
    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

    /// @dev 所有者启用或禁用操作员时触发。（操作员可管理所有者所持有的NFTs）
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /// @notice 统计所持有的NFTs数量
    /// @dev NFT 不能分配给零地址，查询零地址同样会异常
    /// @param _owner ： 待查地址
    /// @return 返回数量，也许是0
    function balanceOf(address _owner) external view returns (uint256);

    /// @notice 返回所有者
    /// @dev NFT 不能分配给零地址，查询零地址抛出异常
    /// @param _tokenId NFT 的id
    /// @return 返回所有者地址
    function ownerOf(uint256 _tokenId) external view returns (address);

    /// @notice 将NFT的所有权从一个地址转移到另一个地址
    /// @dev 如果`msg.sender` 不是当前的所有者（或授权者）抛出异常
    /// 如果 `_from` 不是所有者、`_to` 是零地址、`_tokenId` 不是有效id 均抛出异常。
    ///  当转移完成时，函数检查  `_to` 是否是合约，如果是，调用 `_to`的 `onERC721Received` 并且检查返回值是否是 `0x150b7a02` (即：`bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`)  如果不是抛出异常。
    /// @param _from ：当前的所有者
    /// @param _to ：新的所有者
    /// @param _tokenId ：要转移的token id.
    /// @param data : 附加额外的参数（没有指定格式），传递给接收者。
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

    /// @notice 将NFT的所有权从一个地址转移到另一个地址，功能同上，不带data参数。
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice 转移所有权 -- 调用者负责确认`_to`是否有能力接收NFTs，否则可能永久丢失。
    /// @dev 如果`msg.sender` 不是当前的所有者（或授权者、操作员）抛出异常
    /// 如果 `_from` 不是所有者、`_to` 是零地址、`_tokenId` 不是有效id 均抛出异常。
    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice 更改或确认NFT的授权地址
    /// @dev 零地址表示没有授权的地址。
    ///  如果`msg.sender` 不是当前的所有者或操作员
    /// @param _approved 新授权的控制者
    /// @param _tokenId ： token id
    function approve(address _approved, uint256 _tokenId) external payable;

    /// @notice 启用或禁用第三方（操作员）管理 `msg.sender` 所有资产
    /// @dev 触发 ApprovalForAll 事件，合约必须允许每个所有者可以有多个操作员。
    /// @param _operator 要添加到授权操作员列表中的地址
    /// @param _approved True 表示授权, false 表示撤销
    function setApprovalForAll(address _operator, bool _approved) external;

    /// @notice 获取单个NFT的授权地址
    /// @dev 如果 `_tokenId` 无效，抛出异常。
    /// @param _tokenId ：  token id
    /// @return 返回授权地址， 零地址表示没有。
    function getApproved(uint256 _tokenId) external view returns (address);

    /// @notice 查询一个地址是否是另一个地址的授权操作员
    /// @param _owner 所有者
    /// @param _operator 代表所有者的授权操作员
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}

interface ERC165 {
    /// @notice 是否合约实现了接口
    /// @param interfaceID  ERC-165定义的接口id
    /// @dev 函数要少于  30,000 gas.
    /// @return 合约实现了 `interfaceID`（不为  0xffffffff）返回`true` ， 否则false.
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

如果合约（应用）要接受NFT的安全转账，则**必须实现以下接口**。

```solidity
/// @dev 按 ERC-165 标准，接口id为 0x150b7a02.
interface ERC721TokenReceiver {
    /// @notice 处理接收NFT
    /// @dev ERC721智能合约在`transfer`完成后，在接收这地址上调用这个函数。
    /// 函数可以通过revert 拒绝接收。返回非`0x150b7a02` 也同样是拒绝接收。
    /// 注意: 调用这个函数的 msg.sender是ERC721的合约地址
    /// @param _operator ：调用 `safeTransferFrom` 函数的地址。
    /// @param _from ：之前的NFT拥有者
    /// @param _tokenId ： NFT token id
    /// @param _data ： 附加信息
    /// @return 正确处理时返回 `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
    function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
}
```

以下元信息扩展是可选的（查看后面的“说明”部分），但是可以提供一些资产代表的信息以便查询。

```solidity
/// @title ERC-721非同质化代币标准, 可选元信息扩展
/// @dev See https://learnblockchain.cn/docs/eips/eip-721.html
///  Note: 按 ERC-165 标准，接口id为  0x5b5e139f.
interface ERC721Metadata /* is ERC721 */ {
    /// @notice NFTs 集合的名字
    function name() external view returns (string _name);

    /// @notice NFTs 缩写代号
    function symbol() external view returns (string _symbol);

    /// @notice 一个给定资产的唯一的统一资源标识符(URI)
    /// @dev 如果 `_tokenId` 无效，抛出异常. URIs在 RFC 3986 定义，
    /// URI 也许指向一个 符合 "ERC721 元数据 JSON Schema" 的 JSON 文件
    function tokenURI(uint256 _tokenId) external view returns (string);
}
```

以下是 "ERC721 元数据 JSON Schema" 描述：

```json
{
    "title": "Asset Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "指示NFT代表什么"
        },
        "description": {
            "type": "string",
            "description": "描述NFT 代表的资产"
        },
        "image": {
            "type": "string",
            "description": "指向NFT表示资产的资源的URI（MIME 类型为 image/*） ， 可以考虑宽度在 320 到 1080 像素之间，宽高比在 1.91:1 到 4:5 之间的图像。
        }
    }
}
```

以下枚举扩展信息是可选的（查看后面的“说明”部分），但是可以提供 NFTs 的完整列表，以便NFT可被发现。

```solidity
/// @title ERC-721非同质化代币标准枚举扩展信息
/// @dev See https://learnblockchain.cn/docs/eips/eip-721.html
///  Note: 按 ERC-165 标准，接口id为 0x780e9d63.
interface ERC721Enumerable /* is ERC721 */ {
    /// @notice  NFTs 计数
    /// @return  返回合约有效跟踪（所有者不为零地址）的 NFT数量
    function totalSupply() external view returns (uint256);

    /// @notice 枚举索引NFT
    /// @dev 如果 `_index` >= `totalSupply()` 则抛出异常
    /// @param _index 小于 `totalSupply()`的索引号
    /// @return 对应的token id（标准不指定排序方式)
    function tokenByIndex(uint256 _index) external view returns (uint256);

    /// @notice 枚举索引某个所有者的 NFTs
    /// @dev  如果 `_index` >= `balanceOf(_owner)` 或 `_owner` 是零地址，抛出异常
    /// @param _owner 查询的所有者地址
    /// @param _index 小于 `balanceOf(_owner)` 的索引号
    /// @return 对应的token id （标准不指定排序方式)
    function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);
}
```



### 警告事项
0.4.20版本的Solidity接口语法表达力不足以记录 ERC-721 标准。遵守 ERC-721 的合约还必须遵守以下规则：

Solidity问题#3412：上述接口为每个函数包含了明确的可变性保证。可变性保证从弱到强依次为：可支付的（payable）、隐式不可支付的（implicit nonpayable）、视图（view）和纯（pure）。你的实现必须满足这个接口中的可变性保证，并且你可以满足更强的保证。例如，这个接口中的可支付函数可以在你的合约中实现为不可支付的（没有指定状态可变性）。我们预期Solidity的后续版本将允许你的更严格的合约从这个接口继承，但是0.4.20版本的一个变通办法是你可以编辑这个接口，在从你的合约继承之前添加更严格的可变性。

Solidity问题#3419：实现ERC721Metadata或ERC721Enumerable的合约也必须实现ERC721。ERC-721实现了接口ERC-165的要求。

Solidity问题#2330：如果这个规范中显示一个函数为external，则合约如果使用public可见性将是合规的。作为0.4.20版本的一个变通办法，你可以编辑这个接口，在从你的合约继承之前切换到public。

Solidity问题#3494、#3544：使用this.*.selector被Solidity标记为警告，Solidity的未来版本将不会将其标记为错误。

如果Solidity的新版本允许在代码中表达这些警告事项，那么这个EIP可能会被更新，并且这些警告事项将被移除，这样将等同于原始规范。

### 理由
有许多提议的以太坊智能合约用途依赖于跟踪可区分的资产。现有的或计划中的非同质化代币（NFT）的例子包括Decentraland中的土地、CryptoPunks中的朋克以及使用DMarket或EnjinCoin等系统的游戏中的物品。未来的用途包括跟踪现实世界中的资产，比如房地产（像Ubitquity或Propy这样的公司所设想的）。在这些案例中，关键的一点是这些项目不能作为账本中的数字“混在一起”，而是每项资产必须分别并且原子性地跟踪其所有权。不论这些资产的性质如何，如果我们有一个标准化的接口，允许跨功能的资产管理和销售平台，生态系统将变得更强大。

#### “NFT”一词的选择

“NFT”这个词在几乎所有接受调查的人中都是令人满意的，并且广泛适用于广阔的可区分数字资产宇宙。我们认识到“契据”（deed）对于这个标准的某些应用非常有描述性（特别是，物理财产）。

考虑过的替代词：可区分资产、标题、代币、资产、股权、票据。



#### NFT标识符

每个NFT在ERC-721智能合约内由一个唯一的uint256 ID标识。这个识别号码在合约的生命周期内不应改变。然后（合约地址，uint256 tokenId）对将是以太坊链上特定资产的全球唯一且完全合格的标识符。虽然一些ERC-721智能合约可能会发现从ID 0开始，然后为每个新NFT简单地递增1是方便的，但调用者不应假设ID号码有任何特定的模式，并且必须将ID视为一个“黑箱”。还要注意，NFT可能会变得无效（被销毁）。请参阅支持的枚举接口的枚举函数。

选择uint256允许广泛的应用，因为UUID和sha3哈希可以直接转换为uint256。



#### 转移机制

ERC-721标准化了一个安全的转移函数safeTransferFrom（有和没有字节参数的重载）和一个不安全的函数transferFrom。转移可以由以下实体启动：

- NFT的所有者
- NFT的批准地址
- 当前NFT所有者的授权操作者

此外，授权操作者可以为NFT设置批准地址。这为钱包、经纪人和拍卖应用程序提供了强大的工具集，可以快速使用大量的NFT。

转移和接受函数的文档仅指定了交易必须抛出的条件。你的实现在其他情况下也可能抛出。这允许实现达到有趣的结果：

如果合约暂停，禁止转移——先例，CryptoKitties部署的合约，第611行
阻止某些地址接收NFT——先例，CryptoKitties部署的合约，第565、566行
不允许不安全的转移——transferFrom除非_to等于msg.sender或countOf(_to)非零或之前非零（因为这些情况是安全的）
向交易双方收费——在调用approve时要求支付，如果它之前是零地址，现在是非零地址，或者反之，则退还支付，调用任何转移函数时要求支付，要求转移参数_to等于msg.sender，要求转移参数_to是NFT的批准地址
只读NFT注册表——总是从safeTransferFrom、transferFrom、approve和setApprovalForAll抛出
失败的交易将抛出异常，这是在ERC-223、ERC-677、ERC-827和OpenZeppelin的SafeERC20.sol实现中确定的最佳实践。ERC-20定义了一个允许额度功能，这在被调用并稍后修改为不同金额时会导致问题，如OpenZeppelin问题#438。在ERC-721中，没有允许额度，因为每个NFT都是唯一的，数量是无或一。因此我们收到了ERC-20原始设计的好处，而没有后来发现的问题。

创建NFT（“铸造”）和销毁NFT（“燃烧”）没有包含在规范中。你的合约可以通过其他方式实现这些。请参阅事件文档，了解在创建或销毁NFT时你的职责。

我们质疑onERC721Received上的operator参数是否必要。在我们能想象到的所有情况下，如果操作员很重要，那么该操作员可以先把代币转移到他们自己那里，然后再发送出去——然后他们就会成为from地址。这看起来有些牵强，因为我们认为操作员是代币的临时所有者（而转移到他们自己是多余的）。当操作员发送代币时，是操作员根据自己的意愿行事，而不是代表代币持有者行事。这就是为什么操作员和前一个代币所有者对代币接收者都很重要。

考虑过的替代方案：只允许两步ERC-20风格的交易，要求转移函数永不抛出，要求所有函数返回一个布尔值来指示操作的成功。

### ERC-165接口

我们选择了标准接口检测（ERC-165）来公开ERC-721智能合约支持的接口。

未来的EIP可能会为合约创建一个接口的全局注册表。我们强烈支持这样的EIP，它将允许你的ERC-721实现通过委托到一个单独的合约来实现ERC721Enumerable、ERC721Metadata或其他接口。

关于枚举扩展的Gas和复杂度

这个规范考虑了管理少数和任意大量NFT的实现。如果你的应用能够增长，那么避免在你的代码中使用for/while循环（见CryptoKitties赏金问题#4）。这些表明你的合约可能无法扩展，并且随着时间的推移，gas成本将无限上升。

我们已经部署了一个合约，XXXXERC721，在测试网上，它实例化并跟踪340282366920938463463374607431768211456个不同的契据（2^128）。这足以为每个以太坊账户所有者分配每个IPV6地址，或跟踪总计大小为地球一半的几微米大小的纳米机器人的所有权。你可以从区块链中查询它。并且每个函数的gas都比查询ENS少。

这个例子清楚地表明：ERC-721标准是可扩展的。

考虑过的替代方案：如果资产枚举函数需要for循环，则移除它，从枚举函数返回Solidity数组类型。

### 隐私

在动机部分确定的钱包/经纪人/拍卖人强烈需要识别一个所有者拥有哪些NFT。

可能有趣的是考虑一个用例，NFT不是可枚举的，比如一个私人的财产所有权注册表，或者一个部分私有的注册表。然而，隐私是无法实现的，因为攻击者可以简单地（！）对每个可能的tokenId调用ownerOf。

元数据选择（元数据扩展）

我们在元数据扩展中要求了名称和符号函数。我们审查的每个代币EIP和草案（ERC-20、ERC-223、ERC-677、ERC-777、ERC-827）都包括了这些函数。

我们提醒实现作者，如果你们反对使用这种机制，空字符串是名称和符号的有效响应。我们还提醒每个人，任何智能合约都可以使用与你合约相同的名称和符号。客户端如何确定哪些ERC-721智能合约是著名的（规范的）不在本标准的范围之内。

提供了一种将NFT与URIs关联的机制。我们期望许多实现将利用这一点为每个NFT提供元数据。图像大小建议是从Instagram那里借鉴的，他们可能非常了解图像的可用性。URI可以是可变的（即它会随着时间改变）。我们考虑了一个代表房屋所有权的NFT，在这种情况下，关于房屋的元数据（图像、居住者等）自然会改变。

元数据作为字符串值返回。目前，这只适用于从web3调用，不适用于其他合约。这是可以接受的，因为我们还没有考虑一个在区块链上的应用会查询此类信息的用例。

考虑过的替代方案：将每个资产的所有元数据放在区块链上（太昂贵），使用URL模板查询元数据部分（URL模板不是所有URL方案都适用，特别是P2P URL），multiaddr网络地址（不够成熟）

社区共识

在原始ERC-721问题上发生了大量讨论，此外，我们在Gitter上举行了第一次现场会议，有很好的代表性并且宣传得很好（在Reddit上，在Gitter #ERC频道和原始ERC-721问题上）。感谢参与者：

@ImAllInNow Rob，来自DECGaming / 2月7日密歇根以太坊聚会的演讲者
@Arachnid Nick Johnson
@jadhavajay Ajay Jadhav，来自AyanWorks
@superphly Cody Marx Bailey - XRAM Capital / 1月20日黑客马拉松分享 / 联合国未来金融黑客马拉松。
@fulldecent William Entriken
第二次活动在ETHDenver 2018举行，讨论可区分资产标准（笔记将被发布）。

我们在这个过程中非常包容，并邀请任何有问题或贡献的人加入我们的讨论。然而，这个标准只支持在此列出的确定用例。

向后兼容性
我们采用了来自ERC-20规范的balanceOf、totalSupply、name和symbol的语义。一个实现也可以包括一个返回uint8(0)的decimals函数，如果它的目标是与ERC-20更加兼容，同时支持这个标准。然而，我们发现要求所有ERC-721实现都支持decimals函数是牵强的。

截至2018年2月的示例NFT实现：

CryptoKitties - 与这个标准早期版本兼容。
CryptoPunks - 部分ERC-20兼容，但不容易概括，因为它在合约中直接包含了拍卖功能，并使用明确将资产称为“朋克”的函数名。
Auctionhouse Asset Interface - 作者需要一个通用接口用于Auctionhouse ÐApp（目前搁置）。他的“Asset”合约非常简单，但缺少ERC-20兼容性、approve()功能和元数据。这个工作在EIP-173的讨论中被引用。
注意：“限量版、收藏代币”如Curio Cards和Rare Pepe不是可区分的资产。它们实际上是由各自的智能合约跟踪的个人可替代代币的集合，每个合约都有自己的总供应量（在极端情况下可能是1）。

onERC721Received函数特别解决了旧部署合约可能在某些情况下无意中返回1（真）的问题，即使它们没有实现一个函数（见Solidity DelegateCallReturnValue错误）。通过返回并检查一个魔术值，我们能够区分实际的肯定回应与这些空洞的真值。

测试用例
0xcert ERC-721 Token包括使用Truffle编写的测试用例。

### 实现
0xcert ERC721 - 一个参考实现

MIT许可，所以你可以在项目中自由使用
包括测试用例
活跃的漏洞赏金，如果你发现错误将得到报酬
Su Squares - 一个你可以租用空间并放置图像的广告平台

完成Su Squares漏洞赏金计划，寻找这个标准或其实现的问题
实现了完整的标准和所有可选接口
ERC721ExampleDeed - 一个示例实现

使用OpenZeppelin项目格式实现
XXXXERC721，由William Entriken - 一个可扩展的示例实现

在测试网上部署了10亿个资产，并支持所有带有元数据扩展的查找。这证明可扩展性不是问题。

### 参考文献
标准

1. [ERC-20](https://eips.ethereum.org/EIPS/eip-20) Token Standard.
2. [ERC-165](https://eips.ethereum.org/EIPS/eip-165) Standard Interface Detection.
3. [ERC-173](https://eips.ethereum.org/EIPS/eip-173) Owned Standard.
4. [ERC-223](https://github.com/ethereum/EIPs/issues/223) Token Standard.
5. [ERC-677](https://github.com/ethereum/EIPs/issues/677) `transferAndCall` Token Standard.
6. [ERC-827](https://github.com/ethereum/EIPs/issues/827) Token Standard.
7. Ethereum Name Service (ENS). https://ens.domains
8. Instagram – What’s the Image Resolution? https://help.instagram.com/1631821640426723
9. JSON Schema. https://json-schema.org/
10. Multiaddr. https://github.com/multiformats/multiaddr
11. RFC 2119 Key words for use in RFCs to Indicate Requirement Levels. https://www.ietf.org/rfc/rfc2119.txt

ERC-20代币标准。
ERC-165标准接口检测。
ERC-173拥有标准。
ERC-223代币标准。
ERC-677 transferAndCall代币标准。
ERC-827代币标准。
以太坊名称服务（ENS）。 https://ens.domains 
Instagram - 图像分辨率是多少？ https://help.instagram.com/1631821640426723 
JSON Schema。 https://json-schema.org/ 
Multiaddr。 https://github.com/multiformats/multiaddr 
RFC 2119 RFC中用于表示需求等级的关键词。 https://www.ietf.org/rfc/rfc2119.txt 



#### 问题

原始的ERC-721问题。 https://github.com/ethereum/eips/issues/721 
Solidity问题#2330 - 接口函数是外部的。 https://github.com/ethereum/solidity/issues/2330 
Solidity问题#3412 - 实现接口：允许更严格的可变性。 https://github.com/ethereum/solidity/issues/3412 
Solidity问题#3419 - 接口不能继承。 https://github.com/ethereum/solidity/issues/3419 
Solidity问题#3494 - 编译器关于selector函数的推理不正确。 https://github.com/ethereum/solidity/issues/3494 
Solidity问题#3544 - 不能计算名为transfer的函数的selector。 https://github.com/ethereum/solidity/issues/3544 
CryptoKitties赏金问题#4 - 列出用户拥有的所有小猫是O(n^2)。 https://github.com/axiomzen/cryptokitties-bounty/issues/4 
OpenZeppelin问题#438 - approve方法的实现违反了ERC20标准。 https://github.com/OpenZeppelin/zeppelin-solidity/issues/438 
Solidity DelegateCallReturnValue错误。 https://solidity.readthedocs.io/en/develop/bugs.html#DelegateCallReturnValue 
讨论

Reddit（第一次现场讨论的公告）。 https://www.reddit.com/r/ethereum/comments/7r2ena/friday_119_live_discussion_on_erc_nonfungible/ 
Gitter #EIPs（第一次现场讨论的公告）。 https://gitter.im/ethereum/EIPs?at=5a5f823fb48e8c3566f0a5e7 
ERC-721（第一次现场讨论的公告）。 https://github.com/ethereum/eips/issues/721#issuecomment-358369377 
ETHDenver 2018。 https://ethdenver.com 
NFT实现和其他项目

CryptoKitties。 https://www.cryptokitties.co 
0xcert ERC-721代币。 https://github.com/0xcert/ethereum-erc721 
Su Squares。 https://tenthousandsu.com 
Decentraland。 https://decentraland.org 
CryptoPunks。 https://www.larvalabs.com/cryptopunks 
DMarket。 https://www.dmarket.io 
Enjin Coin。 https://enjincoin.io 
Ubitquity。 https://www.ubitquity.io 
Propy。 https://tokensale.propy.com 
CryptoKitties部署的合约。 https://etherscan.io/address/0x06012c8cf97bead5deae237070f9587f8e7a266d#code 
Su Squares漏洞赏金计划。 https://github.com/fulldecent/su-squares-bounty 
XXXXERC721。 https://github.com/fulldecent/erc721-example 
ERC721ExampleDeed。 https://github.com/nastassiasachs/ERC721ExampleDeed 
Curio Cards。 https://mycuriocards.com 
Rare Pepe。 https://rarepepewallet.com 
Auctionhouse Asset Interface。 https://github.com/dob/auctionhouse/blob/master/contracts/Asset.sol 
OpenZeppelin SafeERC20.sol实现。 https://github.com/OpenZeppelin/zeppelin-solidity/blob/master/contracts/token/ERC20/SafeERC20.sol 

版权
通过CC0放弃版权及相关权利。

引用
请按以下方式引用本文：

William Entriken (@fulldecent)、Dieter Shirley <dete@axiomzen.co>、Jacob Evans <jacob@dekz.net>、Nastassia Sachs <nastassia.sachs@protonmail.com>，“ERC-721：非同质化代币标准，”以太坊改进建议，第721号，2018年1月。[在线系列]。可从：https://eips.ethereum.org/EIPS/eip-721 获取。
